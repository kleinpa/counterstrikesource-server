load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@rules_pkg//:mappings.bzl", "pkg_files")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("//:mapcycle.bzl", "mapcycle_file")
load("//:steamapps.bzl", "BUILD_counterstrikesource_dedicated")

#
# Counter-Strike: Source Layer
#

pkg_tar(
    name = "counterstrikesource_layer",
    srcs = [
        "@counterstrikesource_dedicated//:files",
    ],
    package_dir = "/opt/game",
)

#
# Maps Layer
#

mapcycle_file(
    name = "mapcycle",
    map_archive = "@maps//file",
)

pkg_files(
    name = "mapcycle_files",
    srcs = [":mapcycle"],
    prefix = "/opt/game/cstrike/cfg",
)

pkg_tar(
    name = "maps_layer",
    srcs = [":mapcycle_files"],
    package_dir = "/opt/game/cstrike",
    deps = ["@maps//file"],
)

#
# MetaMod Layer
#

pkg_tar(
    name = "metamod_layer",
    package_dir = "/opt/game/cstrike",
    deps = ["@metamod_linux//file"],
)

#
# SourceMod Layer
#

pkg_tar(
    name = "sourcemod_layer",
    empty_dirs = ["/opt/game/cstrike/addons/sourcemod/configs"],
    package_dir = "/opt/game/cstrike",
    deps = ["@sourcemod_linux//file"],
)

#
# Authorization Layer
#

pkg_tar(
    name = "authorization_layer",
    package_dir = "/opt/game/cstrike",
    deps = ["@auth_by_steam_group_linux//file"],
)

#
# Plugins Layers
#

pkg_tar(
    name = "plugins_layer",
    package_dir = "/opt/game/cstrike",
    deps = [
        "@disable_buyzones//file",
        "@disable_radar//file",
        "@disable_round_timer//file",
        "@ffa_spawns//file",
        "@free_for_all//file",
        "@gungame//file",
        "@map_settings//file",
        "@max_cash//file",
        "@paintball//file",
        "@remove_objectives//file",
        "@respawn//file",
        "@spawn_protection//file",
    ],
)

#
# Config Layer
#

pkg_files(
    name = "config_files",
    srcs = glob(["config/**"]),
    strip_prefix = "config",
)

pkg_tar(
    name = "config_layer",
    srcs = [
        ":config_files",
        ":server.cfg",
    ],
    package_dir = "/opt/game/cstrike/cfg",
)

#
# Final Image
#

pkg_tar(
    name = "entrypoint_layer",
    package_dir = "/",
    symlinks = {"/root/.steam/sdk32/steamclient.so": "/opt/game/bin/steamclient.so"},
)

oci_image(
    name = "base",
    base = "@counterstrikesource-server-steamcmd",
    entrypoint = [
        "/opt/game/srcds_linux",
        "-game",
        "cstrike",
        "+ip",
        "0.0.0.0",
    ],
    env = {"LD_LIBRARY_PATH": "/opt/game:/opt/game/bin"},
    tars = [
        ":counterstrikesource_layer",
    ],
    workdir = "/opt/game",
)

oci_image(
    name = "image",
    base = ":base",
    tars = [
        ":maps_layer",
        ":metamod_layer",
        ":sourcemod_layer",
        # ":authorization_layer",
        ":plugins_layer",
        ":config_layer",
        ":entrypoint_layer",
    ],
)

oci_load(
    name = "image_tarball",
    image = ":image",
    repo_tags = ["counterstrikesource-server:bazel"],
)

oci_push(
    name = "image_push",
    image = ":image",
    remote_tags = [
        "latest",
        "build{}".format(BUILD_counterstrikesource_dedicated),
    ],
    repository = "ghcr.io/kleinpa/counterstrikesource-server",
)

build_test(
    name = "build_test",
    targets = [
        ":image",
    ],
)
